apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

apply from: 'local.gradle'

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

def packageName = 'com.ponyvillelive.pvlmobile'

android {
    compileSdkVersion 23
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId packageName
        minSdkVersion 15
        targetSdkVersion 22

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "PACKAGE_NAME", "\"${packageName}\""
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile 'com.jakewharton.timber:timber:2.5.0'
    compile 'com.jakewharton:butterknife:6.0.0'

    compile 'com.squareup.okhttp:okhttp:2.1.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
    compile 'com.squareup.retrofit:retrofit:1.8.0'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.dagger:dagger:1.2.2'

    compile 'io.reactivex:rxjava:1.0.2'
    compile 'io.reactivex:rxandroid:0.23.0'

    compile 'com.google.android.gms:play-services-base:8.4.0'
    compile 'com.google.android.gms:play-services-cast:8.4.0'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'

    compile 'com.google.code.gson:gson:2.4'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.sothree.slidinguppanel:library:2.0.4'
    compile 'org.lucasr.twowayview:core:1.0.0-SNAPSHOT@aar'
    compile 'org.lucasr.twowayview:layouts:1.0.0-SNAPSHOT@aar'
    compile 'com.melnykov:floatingactionbutton:1.1.0'

    compile 'com.android.support:mediarouter-v7:23.3.0'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:palette-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'

    debugCompile 'com.jakewharton.madge:madge:1.1.1'
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'
    debugCompile 'com.squareup.retrofit:retrofit-mock:1.8.0'
}

def srcDir = 'src'
def gmsJson = 'google-services.json'

task switchToRelease(type: Copy) {
    description 'Copies release google-services.json to the module root'
    from "${srcDir}/main/"
    include "${gmsJson}"
    into '.'
}

task switchToDebug(type: Copy) {
    description 'Copies debug google-services.json to the module root'
    from "${srcDir}/debug"
    include "${gmsJson}"
    into '.'
}

// This actually has to be after our deps because GMS is stupid
apply plugin: 'com.google.gms.google-services'

// And because GMS is _still_ stupid, we need to copy
// the correct config file to the module root each build
afterEvaluate {
    processDebugGoogleServices.dependsOn switchToDebug
    processReleaseGoogleServices.dependsOn switchToRelease
}
